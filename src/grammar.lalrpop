// LALRPOP grammar for the VM front-end.
//
// Adapted from that found in the `amath` crate, with heavy additions.
// https://github.com/vpzomtrrfrt/amath/blob/master/src/grammar.lalrpop

use crate::ast;
use std::str::FromStr;


grammar;


// Factor out comma-separated lists of things.
Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}


// A single item in a map.
MapItem: (String, ast::Expr) = {
    <k:Str> ":" <v:Expr> => (k, v)
}


// Used in list literals and function call arguments.
ExprList = Comma<Expr>;


// So far only used in maps.
MapItems = Comma<MapItem>;


// Top-level expression rule.
//
// This will always be the lowest-precedence group of operators Right
// now, this is the logic operators. This way sloppily-parenthesized
// should do the least surprising thing.
pub Expr: ast::Expr = {
    <a:Expr> "and" <b:Rel> => ast::bin(ast::BinOp::And, a, b),
    <a:Expr> "or"  <b:Rel> => ast::bin(ast::BinOp::Or, a, b),
    <a:Expr> "xor" <b:Rel> => ast::bin(ast::BinOp::Xor, a, b),
    Rel
}


// Relational operators are the next highest precedence.
Rel: ast::Expr = {
    <a:Rel> "<"  <b:Sum> => ast::bin(ast::BinOp::Lt, a, b),
    <a:Rel> ">"  <b:Sum> => ast::bin(ast::BinOp::Gt, a, b),
    <a:Rel> "<=" <b:Sum> => ast::bin(ast::BinOp::Lte, a, b),
    <a:Rel> ">=" <b:Sum> => ast::bin(ast::BinOp::Gte, a, b),
    <a:Rel> "==" <b:Sum> => ast::bin(ast::BinOp::Eq, a, b),
    Sum
};


// Lowest precedence arithmetic operator.
Sum: ast::Expr = {
    <a:Sum> "+" <b:Factor> => ast::bin(ast::BinOp::Add, a, b),
    <a:Sum> "-" <b:Factor> => ast::bin(ast::BinOp::Sub, a, b),
    Factor,
};


Factor: ast::Expr = {
    <a:Factor> "*" <b:Term> => ast::bin(ast::BinOp::Mul, a, b),
    <a:Factor> "/" <b:Term> => ast::bin(ast::BinOp::Div, a, b),
    Exp
};


Exp: ast::Expr = {
    <base:Exp> "^" <exp: InvTerm> => ast::bin(ast::BinOp::Pow, base, exp),
    InvTerm
}


// We want these unary operators applied to a function call to do the
// right thing. I.e. -foo() should parse as (- (foo)), not ((- foo)).
//
// The same thing applies to logical negation.
InvTerm: ast::Expr = {
    "-" <a:InvTerm> => ast::un(ast::UnOp::Neg, a),
    "not" <a:InvTerm> => ast::un(ast::UnOp::Not, a),
    Call
};


// We want indexing and selection to bind before function calls, so that
// foo.bar(a) parses as ((dot foo bar) a), not (dot foo (bar a))
Call: ast::Expr = {
    <func:Call> "(" <args:ExprList> ")" => ast::call(func, args),
    Selection
}


// Ideally, we'd allow arbitrary expression on the left hand side, but
// this results in ambiguity that LALRPOP can't handle.
//
// That means extra parenthesis are required to index or select on the
// result of an expression.
Selection: ast::Expr = {
    <obj:Selection> "." <id:Id> => ast::dot(obj, id.as_str()),
    <obj:Selection> "[" <e:Expr> "]" => ast::index(obj, e),
    Term
}


Term: ast::Expr = {
    Id => ast::Expr::Id(<>),
    Int => ast::Expr::Int(<>),
    Float => ast::Expr::Float(<>),
    Str => ast::Expr::Str(<>),
    Boolean => ast::Expr::Bool(<>),
    "(" <a:Expr> ")" => a,
    "[" <l:ExprList> "]" => ast::list(l),
    "{" <m:MapItems> "}" => ast::map(m),
};


Int: i64 = r"-?[0-9]+" => i64::from_str(<>).unwrap();
Float: f64 = r"-?[0-9]+\.[0-9]+" => f64::from_str(<>).unwrap();
Id: String = r"[A-Za-z_][A-Za-z0-9_]*" => <>.to_owned();
Boolean: bool = {"true" => true, "false" => false};
Str: String = <s:r#""(([^\\"]|\\.)*)""#> => s[1..(s.len() - 1)].to_string();
